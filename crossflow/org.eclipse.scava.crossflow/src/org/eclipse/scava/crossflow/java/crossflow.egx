import "../util.eol";

rule Workflow2App
	transform w : Workflow {
	parameters : w.getParameters("Workflow2App")
	template : "workflow2app.egl"
	target : w.getImplPath() + w.name + "App.java"
	overwrite : false
}

rule Workflow2TaskEnum
	transform w : Workflow {
	parameters : w.getParameters("Workflow2TaskEnum")
	template : "workflow2taskenum.egl"
	target : w.getPath() + w.name + "Tasks.java"
}

rule Config2ConfigSource
	transform t : Type {
	guard : t.eContainer().tasks.collect(c|c.configurations).flatten.asSet().contains(t)
	parameters  {
		var params = t.getParameters("Config2ConfigSource");
		params.put("t", t.createSource());
		return params;
	} 
	template : "task2baseclass.egl"
	target : t.getPath() + t.name + ".java"
}

rule Workflow2Class 
	transform w : Workflow {
	parameters : w.getParameters("Workflow2Class")
	template : "workflow2class.egl"
	target : w.getPath() + w.name + ".java"
}

rule Type2Class 
	transform t : Type {
	guard : t.impl.isUndefined() //and s.csvFile.isUndefined()
	parameters : t.getParameters("Type2Class")
	template : "type2class.egl"
	target : t.getPath() + t.name + ".java"
}

rule CsvSource2Source
	transform t : CsvSource {
	parameters : t.getParameters("CsvSource2Source")
	template : "csvsource2source.egl"
	target : t.getImplPath() + t.name + ".java"	
	overwrite : false
}

rule CsvSink2Sink
	transform t : CsvSink {
	parameters : t.getParameters("CsvSink2Sink")
	template : "csvsink2sink.egl"
	target : t.getImplPath() + t.name + ".java"	
	overwrite : false
}

@greedy
rule CommitmentTask2BaseClass 
	transform t : CommitmentTask {
	guard : t.isJavaTask()
	parameters : t.getParameters("CommitmentTask2BaseClass")
	template : "commitmenttask2baseclass.egl"
	target : t.getPath() + "Commitment" + t.name + "Base.java"	
}

@greedy
rule OpinionatedTask2BaseClass 
	transform t : OpinionatedTask {
	guard : t.isJavaTask()
	parameters : t.getParameters("OpinionatedTask2BaseClass")
	template : "opinionatedtask2baseclass.egl"
	target : t.getPath() + "Opinionated" + t.name + "Base.java"	
}

@greedy
rule Task2BaseClass 
	transform t : Task {
	guard : t.isJavaTask()
	parameters : t.getParameters("Task2BaseClass")
	template : "task2baseclass.egl"
	target : t.getPath() + t.name + "Base.java"	
}

@greedy
rule Task2ImplClass 
	transform t : Task {
	guard :	t.isJavaTask() and not t.isTypeOf(ScriptedTask)
	parameters : t.getParameters("Task2ImplClass")
	template : "task2implclass.egl"
	target : t.getImplPath() + t.name + ".java"
	overwrite : false
}

@greedy
rule Steam2Class 
	transform s : Stream {
	guard : not s.inputOf.isEmpty()
	parameters : s.getParameters("Steam2Class")
	template : "stream2class.egl"
	target : s.getPath() + s.name + ".java"
}

@greedy
rule Steam2Consumer 
	transform s : Stream {
	parameters : s.getParameters("Steam2Consumer")
	template : "stream2consumer.egl"
	target : s.getPath() + s.name + "Consumer.java"
}

operation Type findEligibleConfigurableTasks(){
	return Task.all.select(t|t.configurations.exists(c|c=self));
}

operation Any getPath() {
	var workflow = self;
	if (not self.isTypeOf(Workflow)) workflow = self.eContainer();
	
	var lang = workflow.languages.select(l|l.isJava());
	var langExists = not lang.isEmpty;

	var path = "src-gen/";
	
	if (langExists and lang.first.genOutputFolder.isDefined()) {
		path = lang.first.genOutputFolder + "/";
	}
	
	path = path + workflow.getPackageName().replaceAll("\\.", "/") + "/";
	
	return path;
}

operation Any getImplPath() {
	var workflow = self;
	if (not self.isTypeOf(Workflow)) workflow = self.eContainer();
	
	var lang = workflow.languages.select(l|l.isJava());
	var langExists = not lang.isEmpty;
		
	var path = "src/";
		
	if (langExists and lang.first.outputFolder.isDefined()) {
		path = lang.first.outputFolder + "/";
	}
	
	path = path + workflow.getPackageName().replaceAll("\\.", "/") + "/";

	return path;
}

operation Task isJavaTask() {
	return self.languages.isEmpty() or self.languages.exists(l | l.isJava());
}

@cached
operation Any getParameters(ruleName : String) : Map {
	return self.getParameters(ruleName, "java");
}
