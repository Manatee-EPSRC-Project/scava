rule Workflow2Class 
	transform w : Workflow {
	
	template : "workflow2class.egl"
	
	target : w.getPath() + w.name + ".java"
}

rule CompositeWorkflow2Class 
	transform w : Workflow {
	
	template : "compositeworkflow2class.egl"
	
	target : w.getPath() + "Composite" + w.name + ".java"
}

rule Type2Class 
	transform t : Type {
	
	guard : t.impl.isUndefined() //and s.csvFile.isUndefined()
	
	template : "type2class.egl"
	
	target : t.getPath() + t.name + ".java"
}

rule CsvSource2Source
	transform t : CsvSource {

	parameters : Map{"w" = t.eContainer()}

	template : "csvsource2source.egl"
	
	target : t.getImplPath() + t.name + ".java"	
	
	overwrite : false
}

rule CsvSink2Sink
	transform t : CsvSink {

	parameters : Map{"w" = t.eContainer()}

	template : "csvsink2sink.egl"
	
	target : t.getImplPath() + t.name + ".java"	
	
	overwrite : false
}

@greedy
rule Task2BaseClass 
	transform t : Task {
	
	parameters : Map{"w" = t.eContainer()}
	
	template : "task2baseclass.egl"
	
	target : t.getPath() + t.name + "Base.java"	
}

@greedy
rule Task2ImplClass 
	transform t : Task {
		
	parameters : Map{"w" = t.eContainer()}
	
	template : "task2implclass.egl"
	
	target : t.getImplPath() + t.name + ".java"
	
	overwrite : false
}

@greedy
rule Steam2Class 
	transform s : Stream {
	
	parameters : Map{"w" = s.eContainer()}
	
	template : "stream2class.egl"
	
	target : s.getPath() + s.name + ".java"
}

@greedy
rule Steam2Consumer 
	transform s : Stream {
	
	template : "stream2consumer.egl"
	
	target : s.getPath() + s.name + "Consumer.java"
}

operation Any getPath() {
	var workflow = self;
	if (not self.isTypeOf(Workflow)) workflow = self.eContainer();
	return "src-gen/" + workflow.package.replaceAll("\\.", "/") + "/";
}

operation Any getImplPath() {
	var workflow = self;
	if (not self.isTypeOf(Workflow)) workflow = self.eContainer();
	
	return "src/" + workflow.package.replaceAll("\\.", "/") + "/";
}